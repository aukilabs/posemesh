function(COPY_FILE_WITH_TEXT_REPLACE SOURCE_PATH DESTINATION_PATH)
    list(APPEND OPTION_KEYWORDS)
    list(APPEND SINGLE_VALUE_KEYWORDS)
    list(
        APPEND MULTI_VALUE_KEYWORDS
            REPLACES
    )
    cmake_parse_arguments(ARG "${OPTION_KEYWORDS}" "${SINGLE_VALUE_KEYWORDS}" "${MULTI_VALUE_KEYWORDS}" ${ARGN})
    if(DEFINED ARG_UNPARSED_ARGUMENTS)
        list(GET ARG_UNPARSED_ARGUMENTS 0 FIRST_UNKNOWN_KEYWORD)
        message(FATAL_ERROR "Unknown '${FIRST_UNKNOWN_KEYWORD}' keyword.")
    endif()

    file(REAL_PATH "${SOURCE_PATH}" SOURCE_PATH_ABSOLUTE BASE_DIRECTORY "${CMAKE_CURRENT_SOURCE_DIR}")
    if(NOT EXISTS "${SOURCE_PATH_ABSOLUTE}")
        message(FATAL_ERROR "File '${SOURCE_PATH_ABSOLUTE}' does not exist.")
    endif()
    if(IS_DIRECTORY "${SOURCE_PATH_ABSOLUTE}")
        message(FATAL_ERROR "File '${SOURCE_PATH_ABSOLUTE}' is a directory.")
    endif()

    file(REAL_PATH "${DESTINATION_PATH}" DESTINATION_PATH_ABSOLUTE BASE_DIRECTORY "${CMAKE_CURRENT_BINARY_DIR}")

    if(NOT ARG_REPLACES)
        message(FATAL_ERROR "Keyword 'REPLACES' is not specified or no arguments are specified for 'REPLACES' keyword.")
    endif()
    list(LENGTH ARG_REPLACES REPLACES_LENGTH)
    if("${REPLACES_LENGTH}" MATCHES "[13579]$")
        message(FATAL_ERROR "Number of arguments specified for 'REPLACES' keyword must be even.")
    endif()

    file(READ "${SOURCE_PATH_ABSOLUTE}" FILE_CONTENT)
    math(EXPR FOREACH_STOP "${REPLACES_LENGTH} - 1")
    foreach(REPLACE_INDEX RANGE 0 ${FOREACH_STOP} 2)
        list(GET ARG_REPLACES ${REPLACE_INDEX} TEXT_TO_BE_REPLACED)
        math(EXPR REPLACE_INDEX_NEXT "${REPLACE_INDEX} + 1")
        list(GET ARG_REPLACES ${REPLACE_INDEX_NEXT} TEXT_TO_REPLACE_WITH)
        if(TEXT_TO_BE_REPLACED MATCHES "^\\|MATCH-WORD\\|")
            string(SUBSTRING "${TEXT_TO_BE_REPLACED}" 12 -1 TEXT_TO_BE_REPLACED)
            string(REGEX REPLACE "(^|[^a-zA-Z0-9_])${TEXT_TO_BE_REPLACED}([^a-zA-Z0-9_]|$)" "\\1bfd51e87-badb-4952-9bb8-18e120411a6c\\2" FILE_CONTENT "${FILE_CONTENT}")
            string(REPLACE "bfd51e87-badb-4952-9bb8-18e120411a6c" "${TEXT_TO_REPLACE_WITH}" FILE_CONTENT "${FILE_CONTENT}")
        elseif(TEXT_TO_BE_REPLACED MATCHES "^\\|REGEX\\|")
            string(SUBSTRING "${TEXT_TO_BE_REPLACED}" 7 -1 TEXT_TO_BE_REPLACED)
            string(REGEX REPLACE "${TEXT_TO_BE_REPLACED}" "${TEXT_TO_REPLACE_WITH}" FILE_CONTENT "${FILE_CONTENT}")
        else()
            string(REPLACE "${TEXT_TO_BE_REPLACED}" "${TEXT_TO_REPLACE_WITH}" FILE_CONTENT "${FILE_CONTENT}")
        endif()
    endforeach()
    file(WRITE "${DESTINATION_PATH_ABSOLUTE}" "${FILE_CONTENT}")
    unset(FILE_CONTENT)
endfunction()
